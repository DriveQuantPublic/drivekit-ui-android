apply plugin: 'maven-publish'

publishing {
    repositories {
        maven {
            if (System.env.REPO_PATH) {
                url System.env.REPO_PATH
                if (System.env.MAVEN_LOGIN && System.env.MAVEN_PASSWORD) {
                    credentials {
                        username System.env.MAVEN_LOGIN
                        password System.env.MAVEN_PASSWORD
                    }
                }
            } else {
                url mavenLocal().url
            }
        }
        maven {
            name = "localRepository"
            url = layout.projectDirectory.dir("local-repository")
        }
    }
    publications {
        release(MavenPublication) {
            groupId = "com.drivequant.drivekit"
            artifactId = project.artifactId
            version = android.defaultConfig.versionName

            pom {
                name = "com.drivequant.drivekit:${project.artifactId}"
                version = android.defaultConfig.versionName
                description = "DriveKit SDK for Android"
                url = "https://drivequant.com"

                licenses {
                    license {
                        name = "Commercial licence"
                        url = "https://www.drivequant.com/legal-notices"
                    }
                }

                developers {
                    developer {
                        name = "DriveQuant"
                        email = "developer@drivequant.com"
                        organization = "DriveQuant"
                        organizationUrl = "https://drivequant.com"
                    }
                }

                scm {
                    connection.set("scm:git:private://not-public-repository")
                    developerConnection.set("scm:git:private://not-public-repository")
                    url.set("https://docs.drivequant.com")
                }
            }

            artifact(tasks.named("emptySourcesJar"))
            artifact(tasks.named("emptyJavadocJar"))

            afterEvaluate {
                from components.release
            }
        }
    }
}
